import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
}

android {
    namespace 'dev.kozinski.alusia'
    compileSdk 35

    defaultConfig {
        applicationId "dev.kozinski.alusia"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"

        vectorDrawables {
            useSupportLibrary true
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard.txt'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += '-opt-in=androidx.compose.material3.ExperimentalMaterial3Api'
    }
    buildFeatures {
        compose true
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    testOptions {
        managedDevices {
            devices {
                phone (ManagedVirtualDevice) {
                    device = "Nexus One"
                    apiLevel = 34
                    systemImageSource = "aosp-atd"
                }
            }
        }
    }
}

dependencies {
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat.ktx
    implementation libs.androidx.activity.ktx
    implementation platform(libs.androidx.compose.bom)
    implementation libs.bundles.androidx.compose
    implementation libs.androidx.compose.material.icons
    implementation libs.bundles.androidx.compose.navigation

    debugRuntimeOnly libs.bundles.androidx.compose.debug

    androidTestRuntimeOnly libs.androidx.test.runner
    androidTestImplementation libs.bundles.androidx.test
    androidTestImplementation libs.androidx.lifecycle.common
}

tasks.register("outputBuildToolsVersion") {
    doFirst {
        def get = layout.buildDirectory.dir("outputs/versions").get()
        def file = get.file("build-tools").asFile
        file.parentFile.mkdirs()
        file.createNewFile()
        file.write(android.buildToolsVersion)
    }
}
